plugins {
    id "nebula.deb" version "4.9.3"
    id "java"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'org.jbibtex:jbibtex:1.0.18'
    compile 'com.google.guava:guava:29.0-jre'
    compile 'args4j:args4j:2.33'
    compile 'org.slf4j:slf4j-api:1.7.30'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'ch.qos.logback:logback-core:1.2.3'
    compile 'commons-io:commons-io:2.8.0'
    compile('org.reflections:reflections:0.9.12') {
        exclude(module: 'guava')
    }
    compile 'com.ibm.icu:icu4j:63.2'
    compile 'org.freemarker:freemarker:2.3.30'
    compile('org.eclipse.mylyn.docs:org.eclipse.mylyn.wikitext.asciidoc:3.0.38') {
        exclude(module: 'guava')
    }
    compile('org.eclipse.mylyn.docs:org.eclipse.mylyn.wikitext.confluence:3.0.38') {
        exclude(module: 'guava')
    }
    compile('org.eclipse.mylyn.docs:org.eclipse.mylyn.wikitext:3.0.38') {
        exclude(module: 'guava')
    }
    compile('org.eclipse.mylyn.docs:org.eclipse.mylyn.wikitext.html:3.0.38') {
        exclude(module: 'guava')
    }
    compile('org.eclipse.mylyn.docs:org.eclipse.mylyn.wikitext.markdown:3.0.38') {
        exclude(module: 'guava')
    }
    compile('org.eclipse.mylyn.docs:org.eclipse.mylyn.wikitext.mediawiki:3.0.38') {
        exclude(module: 'guava')
    }
    compile('org.eclipse.mylyn.docs:org.eclipse.mylyn.wikitext.textile:3.0.38') {
        exclude(module: 'guava')
    }
    compile('org.eclipse.mylyn.docs:org.eclipse.mylyn.wikitext.tracwiki:3.0.38') {
        exclude(module: 'guava')
    }
    compile('org.eclipse.mylyn.docs:org.eclipse.mylyn.wikitext.twiki:3.0.38') {
        exclude(module: 'guava')
    }
    compile 'org.apache.commons:commons-lang3:3.11'
    compile 'org.apache.commons:commons-text:1.9'
    compile 'org.commonmark:commonmark:0.17.1'
    compile 'org.commonmark:commonmark-ext-gfm-tables:0.17.1'
    compile 'org.commonmark:commonmark-ext-autolink:0.17.1'
    compile 'org.commonmark:commonmark-ext-gfm-strikethrough:0.17.1'
    compile 'org.commonmark:commonmark-ext-heading-anchor:0.17.1'
    compile 'org.commonmark:commonmark-ext-ins:0.17.1'

    compile('org.mapdb:mapdb:3.0.8') {
        exclude(module: 'guava')
    }
    compile 'org.jsoup:jsoup:1.13.1'
    compile 'org.yaml:snakeyaml:1.27'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    compileOnly group: 'org.projectlombok', name: 'lombok', version:'1.18.18'
    annotationProcessor 'org.projectlombok:lombok:1.18.18'
}

task packageDeb(type: Deb) {
    packageName = 'xdocc'
    version = '2.0.3'
    release = 1
    packager = 'Thomas Bocek <tom@xdocc.net>'
    summary = 'A static HTML site generator that compiles various document formats (markdown, wiki (mediawiki, tracwiki, twiki), confluence, textile, ODT, Tex, docx, ...) and images to HTML'
    suggests ('pandoc', '1.16.0', GREATER | EQUAL)

    requires('default-jre-headless|default-jdk-headless', '1.8', GREATER | EQUAL)

    preInstallFile file('src/deb/control/preinst')
    postInstallFile file('src/deb/control/postinst')
    preUninstallFile file('src/deb/control/prerm')

    from(jar.outputs.files) {
        into '/usr/lib/xdocc'
    }

    from('src/deb/shell/xdocc.sh') {
        into '/usr/lib/xdocc'
        fileMode = 0755
    }

    from('src/deb/shell/xdocc.service') {
        into '/etc/systemd/system'
    }

    from(configurations.compile) {
        into '/usr/lib/xdocc/libs'
    }

    from('src/deb/share') {
        into '/usr/share/xdocc'
    }
}

jar {
    manifest {
        attributes(
                'Class-Path': configurations.compile.collect { 'libs/'+it.getName() }.join(' '),
                'Main-Class': 'net.xdocc.Service')
    }
}
